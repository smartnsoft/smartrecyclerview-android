apply plugin: "com.android.library"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"

android
{
  compileSdkVersion 27

  defaultConfig
  {
    minSdkVersion 14
    targetSdkVersion 27

    versionCode 13
    versionName "1.5"
  }

  buildTypes
  {
    release
    {
      minifyEnabled false
      zipAlignEnabled  true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies
{
  // Android
  implementation ("com.android.support:recyclerview-v7:27.0.2")
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  title = "SmartRecyclerView API"
  description "Javadoc for the SmartRecyclerView"
  source = android.sourceSets.main.java.srcDirs

  options.charSet = "UTF-8"
  options.overview = "src/main/javadoc/overview.html"
  options.links = ["http://docs.oracle.com/javase/7/docs/api/", "http://d.android.com/reference/"]

  classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
  // options.encoding = 'UTF-8'
}

artifacts
{
  archives javadocJar
  archives sourcesJar
}

def pomConfig = {
  licenses
  {
    license
    {
      name "MIT License (MIT)"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
}

publishing
{
  publications
  {
    ReleasePublication(MavenPublication)
    {
      artifact sourcesJar
      artifact javadocJar
      groupId "com.smartnsoft"
      artifactId "recyclerview"
      version android.defaultConfig.versionName

      pom.withXml
      {
        def root = asNode()
        root.appendNode("description", "Enhanced RecyclerView with wrappers system")
        root.appendNode("name", "recyclerview")
        root.appendNode("url", "https://github.com/smartnsoft/smartrecyclerview-android")
        root.children().last() + pomConfig
      }
    }
  }
}

//https://gist.github.com/bugs84/b7887fb5d7f9f2d484b8
publishing.publications.all
{
  pom.withXml
  {
    asNode().dependencies."*".findAll()
    {
      it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
        dep.name == it.artifactId.text()
      }
    }.each
    {
      it.scope*.value = "compile"
    }
  }
}

bintray
{
  publications = ["ReleasePublication"]

  pkg
  {
    repo = "maven"
    name = "recyclerview"
    userOrg = "smartnsoft"
    licenses = ["MIT"]
    vcsUrl = "https://github.com/smartnsoft/smartrecyclerview-android.git"
    publish = true

    version
    {
      name = android.defaultConfig.versionName
      desc = "Enhanced RecyclerView with wrappers system"
      vcsTag = "smartrecyclerview-" + android.defaultConfig.versionName
    }
  }
}

uploadArchives
{
  repositories
  {
    mavenDeployer
    {
      repository(url: "https://api.bintray.com/maven/smartnsoft/maven/smartrecyclerview/;publish=1")
      {
        authentication(userName: findProperty("bintrayUsername") ?: "", password: findProperty("bintrayKey") ?: "")
      }

      snapshotRepository(url: nexusSnapshotUrl)
      {
        authentication(userName: nexusUsername, password: nexusPassword)
      }

      pom.project
      {
        name "recyclerview"
        groupId "com.smartnsoft"
        artifactId "recyclerview"
        version android.defaultConfig.versionName
        packaging "aar"
      }
    }
  }
}
